# Identify 3GlyDemissidine # with checking  the precursor mass, 3GlyDemissidine #
QUERYNAME = TriGlySolanidaneAcetate;
DEFINE pr3GlySolanidane = 'C[44..47] H[70..78] O[14..18] N[1..1]' WITH DBR = (9.5,11.5), CHG = +1;
DEFINE Solanidane = 'C[27..27] H[42..44] O[1..2] N[1..1]' WITH DBR = (7.5,8.5), CHG = +1;

IDENTIFY
	# mark 3GlyDemissidine precursor mass
	pr3GlySolanidane IN MS1+ AND
	Solanidane IN MS2+

SUCHTHAT
Solanidane.chemsc[db] - 5.5 == pr3GlySolanidane.chemsc[db] - 9.5

REPORT 
	MASS = "%4.4f" % (pr3GlySolanidane.mass);
	ERROR = "%2.2fppm" % (pr3GlySolanidane.errppm);
	SUMCOMPOSITION = pr3GlySolanidane.chemsc;
	NAME = "pr3GlySolanidane [%d:%d:%d]" % (pr3GlySolanidane.chemsc[C] - 44, pr3GlySolanidane.chemsc[db] - 9.5, pr3GlySolanidane.chemsc[O] - 14);
	INTENS = sumIntensity(pr3GlySolanidane.intensity);

	MASSFrag = "%4.4f" % (Solanidane.mass);
	NAMEFrag = "Solanidane [%d:%d:%d]" % (Solanidane.chemsc[C] - 27, Solanidane.chemsc[db] - 7.5, Solanidane.chemsc[O] - 1);
	INTENSFrag = sumIntensity(Solanidane.intensity);
	Difference = ((pr3GlySolanidane.mass) - (Solanidane.mass));
	SUMCOMPOSITIONFrag = (pr3GlySolanidane.chemsc - Solanidane.chemsc);
	ERRORFrag = "%2.2fppm" % (Solanidane.errppm);;

################ end script ##################