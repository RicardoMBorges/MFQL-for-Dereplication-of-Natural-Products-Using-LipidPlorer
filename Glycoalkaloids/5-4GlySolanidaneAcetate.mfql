# Identify 4GlyDemissidine # with checking  the precursor mass, 4GlyDemissidine #
QUERYNAME = TetraGlySolanidaneAcetate;
DEFINE pr4GlySolanidane = 'C[49..53] H[78..88] O[18..23] N[1..1]' WITH DBR = (10.5,12.5), CHG = +1;
DEFINE Solanidane = 'C[27..27] H[42..44] O[1..2] N[1..1]' WITH DBR = (7.5,8.5), CHG = +1;

IDENTIFY
	# mark 4GlyDemissidine precursor mass
	pr4GlySolanidane IN MS1+ AND
	Solanidane IN MS2+

SUCHTHAT
Solanidane.chemsc[db] - 5.5 == pr4GlySolanidane.chemsc[db] - 10.5

REPORT 
	MASS = "%4.4f" % (pr4GlySolanidane.mass);
	ERROR = "%2.2fppm" % (pr4GlySolanidane.errppm);
	SUMCOMPOSITION = pr4GlySolanidane.chemsc;
	NAME = "pr4GlySolanidane [%d:%d:%d]" % (pr4GlySolanidane.chemsc[C] - 49, pr4GlySolanidane.chemsc[db] - 10.5, pr4GlySolanidane.chemsc[O] - 18);
	INTENS = sumIntensity(pr4GlySolanidane.intensity);

	MASSFrag = "%4.4f" % (Solanidane.mass);
	NAMEFrag = "Solanidane [%d:%d:%d]" % (Solanidane.chemsc[C] - 27, Solanidane.chemsc[db] - 7.5, Solanidane.chemsc[O] - 1);
	INTENSFrag = sumIntensity(Solanidane.intensity);
	Difference = ((pr4GlySolanidane.mass) - (Solanidane.mass));
	SUMCOMPOSITIONFrag = (pr4GlySolanidane.chemsc - Solanidane.chemsc);
	ERRORFrag = "%2.2fppm" % (Solanidane.errppm);;

################ end script ##################