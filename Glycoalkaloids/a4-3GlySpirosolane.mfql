# Identify TriGlySolasodine # with checking  the precursor mass, TriGlySolasodine #
QUERYNAME = TriGlySpirosolane;
DEFINE pr3GlySpirosolane = 'C[42..45] H[68..76] O[14..18] N[1..1]' WITH DBR = (8.5,10.5), CHG = +1;
DEFINE Spirosolane = 'C[27..27] H[44..46] O[2..3] N[1..1]' WITH DBR = (5.5,7.5), CHG = +1;

IDENTIFY
	# mark  precursor mass
	pr3GlySpirosolane IN MS1+ AND
	Spirosolane IN MS2+

SUCHTHAT
Spirosolane.chemsc[db] - 5.5 == pr3GlySpirosolane.chemsc[db] - 8.5

REPORT 
	MASS = "%4.4f" % (pr3GlySpirosolane.mass);
	ERROR = "%2.2fppm" % (pr3GlySpirosolane.errppm);
	SUMCOMPOSITION = pr3GlySpirosolane.chemsc;
	NAME = "pr3GlySpirosolane [%d:%d:%d]" % (pr3GlySpirosolane.chemsc[C] - 42, pr3GlySpirosolane.chemsc[db] - 8.5, pr3GlySpirosolane.chemsc[O] - 14);
	INTENS = sumIntensity(pr3GlySpirosolane.intensity);

	MASSFrag = "%4.4f" % (Spirosolane.mass);
	NAMEFrag = "Spirosolane [%d:%d:%d]" % (Spirosolane.chemsc[C] - 27, Spirosolane.chemsc[db] - 5.5, Spirosolane.chemsc[O] - 2);
	INTENSFrag = sumIntensity(Spirosolane.intensity);
	Difference = ((pr3GlySpirosolane.mass) - (Spirosolane.mass));
	SUMCOMPOSITIONFrag = (pr3GlySpirosolane.chemsc - Spirosolane.chemsc);
	ERRORFrag = "%2.2fppm" % (Spirosolane.errppm);;

################ end script ##################