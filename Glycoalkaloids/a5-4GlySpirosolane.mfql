# Identify TetraGlySolasodine # with checking  the precursor mass, TetraGlySolasodine #
QUERYNAME = TetraGlySpirosolane;
DEFINE pr4GlySpirosolane = 'C[47..51] H[76..86] O[18..23] N[1..1]' WITH DBR = (9.5,11.5), CHG = +1;
DEFINE Spirosolane = 'C[27..27] H[44..46] O[2..3] N[1..1]' WITH DBR = (5.5,7.5), CHG = +1;

IDENTIFY
	# mark  precursor mass
	pr4GlySpirosolane IN MS1+ AND
	Spirosolane IN MS2+

SUCHTHAT
Spirosolane.chemsc[db] - 5.5 == pr4GlySpirosolane.chemsc[db] - 9.5

REPORT 
	MASS = "%4.4f" % (pr4GlySpirosolane.mass);
	ERROR = "%2.2fppm" % (pr4GlySpirosolane.errppm);
	SUMCOMPOSITION = pr4GlySpirosolane.chemsc;
	NAME = "pr4GlySpirosolane [%d:%d:%d]" % (pr4GlySpirosolane.chemsc[C] - 47, pr4GlySpirosolane.chemsc[db] - 9.5, pr4GlySpirosolane.chemsc[O] - 18);
	INTENS = sumIntensity(pr4GlySpirosolane.intensity);

	MASSFrag = "%4.4f" % (Spirosolane.mass);
	NAMEFrag = "Spirosolane [%d:%d:%d]" % (Spirosolane.chemsc[C] - 27, Spirosolane.chemsc[db] - 5.5, Spirosolane.chemsc[O] - 2);
	INTENSFrag = sumIntensity(Spirosolane.intensity);
	Difference = ((pr4GlySpirosolane.mass) - (Spirosolane.mass));
	SUMCOMPOSITIONFrag = (pr4GlySpirosolane.chemsc - Spirosolane.chemsc);
	ERRORFrag = "%2.2fppm" % (Spirosolane.errppm);;

################ end script ##################

